INCLUDE ~Derats_Dawap/Druides/Horde/GW_Preacher.tpa~
COPY	~Derats_Dawap/Clercs/Vie/Copy~					~override~
COPY_EXISTING	~sppr212.spl~				~override/pxdvie11.spl~	WRITE_BYTE 0x34 1 //Ralentissement du poison
COPY_EXISTING	~sppr307.spl~				~override/pxdvie21.spl~					  //Délivrance de la malédiction
				~sppr308.spl~				~override/pxdvie22.spl~ 				  //Délivrance de la paralysie
				~sppr315.spl~				~override/pxdvie23.spl~ 				  //Soins des blessures moyennes
				~sppr317.spl~				~override/pxdvie24.spl~	WRITE_BYTE 0x34 2 //Guérison des maladies
COPY_EXISTING	~sppr401.spl~				~override/pxdvie31.spl~					  //Soins des blessures graves
				~sppr404.spl~				~override/pxdvie32.spl~					  //Neutralisation du poison
				~sppr409.spl~				~override/pxdvie33.spl~				 	  //Spécial : Protection contre la mort
				~sppr413.spl~				~override/pxdvie34.spl~	 				  //Spécial : Protection contre le Plan Négatif
COPY_EXISTING	~sppr417.spl~				~override/pxdvie35.spl~	WRITE_BYTE 0x34 3 //Restitution mineure
COPY_EXISTING	~spwi410.spl~				~override/pxdvie36.spl~	WRITE_BYTE 0x1c 2	WRITE_BYTE 0x34 3	//Spécial : Délivrance de la malédiction
COPY_EXISTING	~sppr502.spl~				~override/pxdvie41.spl~	 				  //Soins des blessures critiques
				~sppr504.spl~				~override/pxdvie42.spl~	 				  //Rappel à la vie
				~sppr514.spl~				~override/pxdvie43.spl~	WRITE_BYTE 0x34 4 //Guérison de masse
COPY_EXISTING	~sppr607.spl~				~override/pxdvie51.spl~	WRITE_BYTE 0x34 5 //Guérison
COPY_EXISTING	~sppr711.spl~				~override/pxdvie61.spl~					  //Régénération
				~sppr712.spl~				~override/pxdvie62.spl~					  //Résurrection
				~sppr713.spl~				~override/pxdvie63.spl~	WRITE_BYTE 0x34 6 //Restauration majeure
OUTER_SET spellId = ((IDS_OF_SYMBOL (~spell~ ~CLERIC_CURE_MODERATE_WOUNDS~)) - 1000)
ACTION_IF ((spellId > 0) AND (FILE_EXISTS_IN_GAME ~sppr%spellId%.spl~)) THEN BEGIN
COPY_EXISTING	~sppr%spellId%.spl~			~override/pxdvie12.spl~	WRITE_BYTE 0x34 1 //IWD Soin des blessures modérées
END
CREATE SPL pxdvie0 //Sort qui supprime tous les sorts contenant soins, régénération, restauration, délivrance et création d'arme (blessure, mise à mort/à mal)
	WRITE_LONG 0x18 THIS | BIT11 | BIT14 | BIT25	// ajoute No LOS required - Non-magical ability - Castable when silenced
	WRITE_SHORT 0x1c 4	// spelltype = innate
	WRITE_LONG  0x34 1	// spelllevel
	LPF ~GW_DEF_OFFSETS_FILE~ STR_VAR GW_file_sig = SPL RET GW_oe GW_oc GW_al GW_fx GW_fi GW_fc GW_min_size END	// DÉFINITION des offsets généraux des fichiers SPL, ITM et CRE.
	LPF ~GW_ADD_HEADERS~ INT_VAR GW_Headers = 1 END
	LPF ~ALTER_SPELL_HEADER~ INT_VAR location = 4 new_header_type = 1 target = 5 range = 0 min_level = 0 speed = 0 projectile = 1 END
	LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 172 target = 1  STR_VAR resource = SPPR307 END //Délivrance de la malédiction
	LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 172 target = 1  STR_VAR resource = SPPR308 END //Délivrance de la paralysie
	LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 172 target = 1  STR_VAR resource = SPPR409 END //Protection contre la mort
	LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 172 target = 1  STR_VAR resource = SPPR413 END //Protection contre le Plan Négatif
	LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 172 target = 1  STR_VAR resource = SPPR411 END //Spécial : Poison
	LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 172 target = 1  STR_VAR resource = SPPR504 END //Rappel à la vie
	LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 172 target = 1  STR_VAR resource = SPPR610 END //Spécial : Dégénérescence
	LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 172 target = 1  STR_VAR resource = SPPR708 END //Doigt de mort
	LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 172 target = 1  STR_VAR resource = SPPR719 END //Symbole : Mort
	LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 172 target = 1  STR_VAR resource = SPPR712 END //Résurrection
	LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 172 target = 1  STR_VAR resource = SPPR402 END //Convocation d'animaux I
	LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 172 target = 1  STR_VAR resource = SPPR501 END //Convocation d'animaux II
	LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 172 target = 1  STR_VAR resource = SPPR602 END //Convocation d'animaux III
	LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 172 target = 1  STR_VAR resource = SPPR601 END //Serviteur aérien
	LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 172 target = 1  STR_VAR resource = SPPR604 END //Conjuration d'Animaux
	LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 172 target = 1  STR_VAR resource = SPPR703 END //Portail
	FOR (level = 7 ; level > 0 ; level = level - 1) BEGIN					// Je commence par le niveau 7 car la fonction ADD_SPELL_CFEFFECT par défaut un effet en première position.
		SET bornesup = level*100 + 99										// Je fixe la borne supérieure du sort à chercher (199 - 299 - 399 ....)
		FOR (spellcode = bornesup ; spellcode > level*100 ; spellcode = spellcode - 1) BEGIN	// Je lis chaque sort SPPR (101 à 199 ; 201 à 299 ...)
			PATCH_IF (FILE_EXISTS_IN_GAME "SPPR%spellcode%.spl") BEGIN							// Test qui permet de gagner du temps de traitement.
				LPF NAME_NUM_OF_SPELL_RES STR_VAR spell_res = EVAL "SPPR%spellcode%" RET spell_num spell_name END	// Fonction de WeiDU 239 qui retourne le numéro du sort et son code du fichier SPELL.ids
				SET GW_opcode_checked = 0			// Test évitant de retrouver le même sort plusieurs fois s'il utilise au moins deux des opcodes à vérifier.
				PATCH_MATCH ~%spell_name%~ WITH
					"CLERIC_.*" BEGIN				// Si le ce code SPELL.IDS commence par "CLERIC_", c'est un nsort de prêtre.
													// Ça permet d'écarter les sorts spéciaux de certines créatures ou de pièges.
						INNER_ACTION BEGIN
							COPY_EXISTING - ~SPPR%spellcode%.spl~ ~override~
								PATCH_FOR_EACH GW_opcode_to_check IN 11 26 32 79 81 98 111 224 BEGIN	// On vérifie l'existance de ces opcodes dans le sort.
									PATCH_IF GW_opcode_checked = 0 BEGIN						// Utilisation du test décrit plus haut
										LPF ~GW_SEARCH_OPCODE~ INT_VAR GW_opcode_to_check RET GW_opcode_checked END	// Valeur retournée	==> 0 = pas d'opcode	-	1 = opcodeprésent dans le fichier
									END
								END
								// Cas particulier de l'opcode Stat: Current HP Modifier [17] :
								// Si paramètre 2 = 0 et paramètre 1 > 0 alors l'effet ajoute des PV.
								// Si paramètre 2 = 2 et paramètre 1 >= 100, alors l'effet ajoute des PV.
								PATCH_IF GW_opcode_checked = 0 BEGIN						// Utilisation du test décrit plus haut
									LPF ~GW_DERAT_FIND_CURE_HP~ INT_VAR GW_opcode_to_check = 17 GW_param1_to_check = 1 GW_param2_to_check = 0 RET GW_opcode_Cure_checked END
									PATCH_IF GW_opcode_Cure_checked = 1 BEGIN				// Si c'est un sort de soins
										SET GW_opcode_checked = 1
									END ELSE BEGIN
										LPF ~GW_DERAT_FIND_CURE_HP~ INT_VAR GW_opcode_to_check = 17 GW_param1_to_check = 100 GW_param2_to_check = 2 RET GW_opcode_Cure_checked END
										PATCH_IF GW_opcode_Cure_checked = 1 BEGIN			// Si c'est un sort de soins
											SET GW_opcode_checked = 1
										END
									END
								END
							BUT_ONLY
						END	// of INNER_ACTION
						PATCH_IF GW_opcode_checked = 1 BEGIN		// Si on a trouvé un sort utilisant les opcodes qui t'intéressent
																	// La fonction ADD_SPELL_CFEFFECT ajoute un casting feature à ton nouveau sort.
							LPF ~ADD_SPELL_CFEFFECT~ INT_VAR opcode = 172 target = 1 timing = 1 STR_VAR resource = EVAL "SPPR%spellcode%" END
						END
						
					END	// of PATCH_MATCH ~%spell_name%~ WITH "CLERIC_.*"
					DEFAULT
				END		// of PATCH_MATCH ~%spell_name%~ WITH
			END			// of PATCH_IF (FILE_EXISTS_IN_GAME "SPPR%spellcode%.spl")
		END				// of FOR (spellcode = bornesup ; spellcode > header*100 ; spellcode = spellcode - 1)
	END					// of FOR (level = 7 ; level > 0 ; level = level - 1)
OUTER_SET spell_num = IDS_OF_SYMBOL (~spell~ ~CLERIC_DESTRUCTION~)
ACTION_IF (spell_num >= 0) THEN BEGIN
   LAF RES_NAME_OF_SPELL_NUM INT_VAR spell_num RET spell_res END
   ACTION_IF FILE_EXISTS_IN_GAME "%spell_res%.spl" BEGIN
      COPY_EXISTING	~pxdvie0.spl~						~override~	LPF ADD_SPELL_CFEFFECT INT_VAR opcode = 172 target = 1  STR_VAR resource = EVAL ~%spell_res%~ END
   END
END